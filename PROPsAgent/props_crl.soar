#####    Author: Bryan Stearns, 2019
#   This code defines the numeric-indifferent preference update process that is
#   based on discretely increasing values associated with the current context,
#   rather than on TD-learning. ("CRL" for Contextal RL)
#
##        OPERATORS
#   (Numeric preferences do not describe operators.)
#   
##        STRUCTURE
#  (<s> ^props-epset (<pe>                  | A set of instructed proposals, fetched from SMEM
#####

decide indifferent-selection -b
decide indifferent-selection -t 0.5

# Set up the learning-rate parameter
# The value can be changed by overwriting this rule.
sp {elaborate*props*pref-weight*learning-rate
    (state <s> ^superstate nil)
-->
    (<s> ^props-pref-learning-rate 0.3)
}

# Define elaboration of input-link reward to the RL interface
sp {set*props*reward*input
    :o-support
    (state <s> ^io.input-link.reward <rwd>
               ^smem <smem>
               ^prop-apply-buffer <b>
               ^props-pref-learning-rate <lr>)
    (<rwd> ^value <rv>
          -^applied <d>)
    (<b> ^apply <ba>)
    (<ba> ^source-delta <d>)
    (<d> ^pref-weight <pw>)
-->
    (<d> ^pref-weight <pw> -
         ^pref-weight (* (+ <pw> <rv>) <lr>))    # TODO: Should probably normalize over count of deltas in context
    (<rwd> ^applied <d>)
    (<smem> ^store-request <d>)
}


# Elaborate the preference
sp {prefer*props*delta*pref-weight
    (state <s> ^operator <o1> +
               ^operator <o2> +) # Could be the same as <o1>
    (<o1> ^delta <d>)
    (<o2> ^epset-name <name>)
    (<d> ^pref-weight <pw>
         ^op-name <name>)
-->
    (<s> ^operator <o1> = <pw>)  # If two proposals are for the same action, combine the prefs
}


# Inhibit things that compete with the current focus
sp {prefer*props*delta*focus-inhibit
    "Inhibit fetching something that would overwrite a current lock"
    (state <s> ^prop-apply-buffer <pab>
               ^operator <o2> +)
    # There is a locked target:
    (<pab> ^epset <be1>
           ^apply <ba1>
           ^epset <be2>)
    (<be1> ^elab-class <class>
           ^wm-target <slot>)
    (<ba1> ^elab-class <class>
           ^active true)
    # The proposed delta uses the same target:
    (<be2> ^delta <d2>)
    (<d2> ^op-name <name2>
          ^wm-target <slot>)
    (<o2> ^name props-load-epset
          ^epset-name <name2>)
-->
    (<s> ^operator <o2> = -0.75)
}

# Prefer reacting to stimuli over imagination
sp {prefer*props*delta*focus-perception
    (state <s> ^operator <o> +
               ^V <v>
               ^delta-info.<d> <info>)
    (<o> ^delta <d>)
    (<info> ^cond-id1.id1 <v>) 
-->
    (<s> ^operator <o> = 0.5)
}

# Default wait operator
sp {propose*props*default-wait
    (state <s> ^superstate nil
              -^operator.name wait)
-->
    (<s> ^operator <o> + = 0.5)
    (<o> ^name wait)
}
