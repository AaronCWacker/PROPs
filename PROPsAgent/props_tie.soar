#####    Author: Bryan Stearns, October 2018
#   This code 
#   This code would not make sense alongside a manual task sequence.
##        OPERATORS
#   
##        STRUCTURE
#  (<ss> ^name props-eval                   | What's in a name?
#        ^
#
#  (<s>  ^name props-resolve-tie
#####


# OVERWRITE FROM props_fetch.soar TO ITERATIVELY FETCH MULTIPLE INSTRUCTIONS BEFORE CHOOSING TO EVALUATE 
sp {elaborate*props*fetched-enough
    "Modified to fetch two instructions before choosing one"
    (state <s> ^name props-eval
               ^props-instructions <ins1>
               ^props-instructions <> <ins1>)
-->
    (<s> ^props-flag fetched-enough
         ^props-flag originals-not-ready)
}
sp {elaborate*props*fetched-enough*allow-plan-b
    "After the first fails, try the second if still around"
    (state <s> ^name props-eval
               ^props-flag has-failed-instructions
               ^props-instructions <ins1>
               -^props-instructions <> <ins1>)
-->
    (<s> ^props-flag fetched-enough
         ^props-flag originals-not-ready)
}


# NAME THE STATE
sp {elaborate*props*tie*state
    (state <s> ^impasse tie)
    #(<o1> ^name props-init-evaluation)
    #(<o2> ^name props-init-evaluation)
-->
    (<s> ^name props-tie)
    (force-learn <s>)
}
sp {elaborate*props*tie*tie-item*from-evaluation
    (state <s> ^name props-tie
               ^item <o1>)
    (<o1> ^name props-init-evaluation
          ^props-instructions <ins>)
-->
    (<s> ^tie-item <ti>)
    (<ti> ^op <o1>
          ^op-instructions <ins>)
}
sp {elaborate*props*tie*tie-item*from-rootstate
    (state <s> ^name props-tie
               ^item <o1>)
    (<o1> ^name <name>
         -^props-instructions )
-->
    (<s> ^tie-item <ti>)
    (<ti> ^op <o1>)
}

### OPERATOR ### 
# props-fetch-op-instructions
##
# This operator fetches the instructions that go with the proposed operator.
# This allows the agent to reason over the nature of the proposal given past experience with learning it.
### 

sp {propose*props*tie*fetch-op-instructions
    "If all we have is the item name, fetch the corresponding instructions"
    (state <s> ^name props-tie
               ^tie-item <ti>)
    (<ti> ^op <o1>
          -^op-instructions )
    (<o1> ^name <name>)
-->
    (<s> ^operator <o> + =)
    (<o> ^name props-fetch-op-instructions
         ^ins-name <name>)
}
sp {apply*props*tie*fetch-op-instructions
    (state <s> ^operator <o>
               ^smem.command <scmd>)
    (<o> ^name props-fetch-op-instructions
         ^ins-name <name>)
-->
    (<scmd> ^query <q>
            ^depth 4)
    (<q> ^prop-type |instruction|
         ^name <name>)
}

### OPERATOR ### 
# props-collect-op-instructions
##
# This operator fetches the instructions that go with the proposed operator.
# This allows the agent to reason over the nature of the proposal given past experience with learning it.
### 

sp {propose*props*tie*collect-op-instructions
    (state <s> ^name props-tie
               ^tie-item <ti>
               ^smem <smem>)
    (<smem> ^result.retrieved <ins>
            ^command.query.prop-type |instruction|)
    (<ti> ^op.name <name>
          -^op-instructions )
    (<ins> ^name <name>)
-->
    (<s> ^operator <o> + >)
    (<o> ^name props-collect-op-instructions
         ^tie-item <ti>
         ^instructions <ins>)
}
sp {apply*props*tie*collect-op-instructions
    (state <s> ^operator <o>
                ^smem.command <scmd>)
    (<o> ^name props-collect-op-instructions
         ^tie-item <ti>
         ^instructions <lti>)
    (<lti> ^name <name>)
-->
    (<ti> ^op-instructions <lti>)
    (write (crlf) |     RETRIEVED | <lti> | - | <name> )
}

###
# Given a structure describing one operator as having better preference,
# return the operator preference and end the substate.
###
sp {elaborate*props*tie*return-pref-struct
    (state <s> ^name props-tie
               ^superstate <ss>
               ^tie-test-result <tr>)
    (<tr> ^result-item <ri1>
          ^result-item <ri2>)
    (<ri1> ^tie-item <ti1>
           ^result pass)
    (<ri2> ^tie-item <ti2>
           ^result fail)
    (<ti1> ^op <o1>)
    (<ti2> ^op <o2>)
-->
    (<ss> ^operator <o1> > <o2>)
}


#### FIXME: Temporary test result
sp {elaborate*props*init-tie*test
    (state <s> ^name props-tie
               ^tie-item <ti1>
               ^tie-item <ti2>)
    (<ti1> ^op-instructions <ins1>)
    (<ti2> ^op-instructions <ins2>)
    (<ins1> ^name <name1>)
    (<ins2> ^name < <name1>)
-->
    (<s> ^tie-test-result <tr>)
    (<tr> ^name name-order-test
          ^result-item <ri1>
          ^result-item <ri2>)
    (<ri1> ^tie-item <ti1>
           ^result pass)
    (<ri2> ^tie-item <ti2>
           ^result fail)
}
