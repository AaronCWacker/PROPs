### SOURCE THIS TO TURN ON (AUTONOMOUS PHASE) CHUNKING OF INSTRUCTED RULES AFTER ALL INSTRUCTION EVALUATION PARTS HAVE BEEN CHUNKED ###

sp {apply*props*force-learn*all*d1
    "Learn the production: when the evalstate is directly under the rootstate."
    (state <s> ^operator.name props-result-success
               ^proposals <ppls>
              -^force-learn-onc-state )
    (<ppls> ^cond-count <ccnt>
            ^act-count <acnt>
            ^propose <p>)
    (<p> ^size {> <ccnt> > <acnt>}    # If greater than both, can only be the combo of both
         ^status success
         ^used true)
-->
    (write (crlf) | *** LEARNING ALL - STATE | <s> |***| )
    (force-learn <s>)
}
sp {apply*props*force-learn*all*d2
    "Learn the production: when the evalstate is depth 2 under the rootstate."
    (state <s> ^operator.name props-result-success
               ^proposals <ppls>
               ^force-learn-onc-state <state>)
    (<ppls> ^cond-count <ccnt>
            ^act-count <acnt>
            ^propose <p>)
    (<p> ^size {> <ccnt> > <acnt>}    # If greater than both, can only be the combo of both
         ^status success
         ^used true)
-->
    (write (crlf) | *** LEARNING ALL - STATE | <state> |***| )
    (force-learn <state>)
}

### TURN OFF ROOTSTATE LEARNING (FROM SPREAD RESULTS) ONLY WHEN NOT READY TO FULLY PROCEDURALIZE ###
# (Note that this use of dont-learn doesn't work in vanilla Soar, though it really should. I modified the kernel so that it turns off force-learn.)
sp {apply*props*dont-learn*snc
    (state <s> ^operator.name props-result-success
               ^proposals <ppls>
              -^force-learn-onc-state )
    (<ppls> ^cond-count <ccnt>
            ^act-count <acnt>)
   -{(<ppls> ^propose <p>)
     (<p> ^size {> <ccnt> > <acnt>}    # If greater than both, can only be the combo of both
          ^status success
          ^used true)}
-->
    (write (crlf) | *** NOT LEARNING STATE | <s> |***| )
    (dont-learn <s>)
}    # Should overwrite props_learn_conds.soar if both used
sp {apply*props*dont-learn*onc
    (state <s> ^operator.name props-result-success
               ^proposals <ppls>
               ^force-learn-onc-state <state>)
    (<ppls> ^cond-count <ccnt>
            ^act-count <acnt>)
   -{(<ppls> ^propose <p>)
     (<p> ^size {> <ccnt> > <acnt>}    # If greater than both, can only be the combo of both
          ^status success
          ^used true)}
-->
    (write (crlf) | *** NOT LEARNING STATE | <state> |***| )
    (dont-learn <state>)
}    # Should overwrite props_learn_conds.soar if both used
