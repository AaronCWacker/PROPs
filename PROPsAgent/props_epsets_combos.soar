
### ON TIE IMPASSE MARK ORIGINAL PROPOSALS FOR LATER ###
sp {elaborate*props*delta-tie*item-indifferent
    "Make all delta proposals indifferent"
    (state <s> ^impasse tie
               ^superstate <ss>)
    (<ss> ^props-epset <pe>)        # Generic justification condition
-->
    (<ss> ^propose-indifferent true)
    (dont-learn <ss>)    # Turn off chunking of full apply until general compositions are finished 
}

sp {apply*props*delta*tied-item*record-unused
    (state <s> ^operator <ocurr>
               ^operator {<o2> <> <ocurr>} +
               ^propose-indifferent true)
    (<ocurr> ^delta.condition <c>)
    (<o2> ^delta <d2>)
    (<d2> ^condition <c>)     # This op will retract because it uses the same condition
-->
    (<d2> ^unused (dc))       # Record the dc so chunk negation conditions are unused in practice
}

# IN SNC SUBSTATE: COMBINE USED OPERATORS THAT HAD TIED
sp {elaborate*props*delta-combine*state
    (state <s> ^impasse no-change
               ^attribute state)
-->
    (<s> ^name props-delta-combine
         ^dont-return true    # Remove this to trigger ending the substate
         ^dc (dc))
    (force-learn <s>)
}


sp {propose*props*delta-combine*combos
    (state <s> ^name props-delta-combine
               -^combos-applied 3)
-->
    (<s> ^operator <o> +)
    (<o> ^name props-delta-combine-combos)
}
# Apply cycle 1: Create the combos
sp {apply*props*delta-tie*combine*step-1
    (state <s> ^operator.name props-delta-combine-combos
              -^combos-applied 1)
-->
    (<s> ^combos-applied 1)
}
sp {apply*props*delta-combine*combos
    (state <s> ^operator.name props-delta-combine-combos
               ^superstate.props-epset <pe>
              -^combos-applied 3  # Prevent re-fires
               ^dc <dc>)
    (<pe> ^delta <d1>
          ^delta {<d2> < <d1>})
    -(<d1> ^unused < <dc>)
    -(<d2> ^unused < <dc>)
-->
    (<s> ^combo <dnew>)
    (<dnew> ^source-delta <d1>
            ^source-delta <d2>)
}
#sp {elaborate*props*delta-combine*combos*conditions
#    (state <s> ^name props-delta-combine
#               ^combo <combo>)
#    (<combo> ^source-delta.condition <c>)
#-->
#    (<combo> ^condition <c>)
#}
sp {elaborate*props*delta*combos*conditions
    (state <s> ^props-epset.delta <d>)
    (<d> ^source-delta.condition <c>)
-->
    (<d> ^condition <c>)
}
sp {elaborate*props*delta-combine*combos*prop-apply-name
    (state <s> ^name props-delta-combine
               ^combo <combo>)
    (<combo> ^source-delta <d1>
             ^source-delta {<d2> < <d1>})
    (<d1> ^prop-apply <dname1>)
    (<d2> ^prop-apply <dname2>)
-->
    (<s> ^combo-literalization <lnew>)
    (<combo> ^prop-apply (concat <dname1> <dname2>)
             ^op-name (concat <dname1> <dname2>))
    (<lnew> ^combo <combo>
            ^literalize (+ 1 (int <dname1>) (int <dname2>)) )
}
# Apply cycle 2: Remove any bad combos
sp {apply*props*delta-tie*combine*step-2
    (state <s> ^operator.name props-delta-combine-combos
               ^combos-applied 1
              -^combos-applied 2)
-->
    (<s> ^combos-applied 2)
}
sp {apply*props*delta-combine*combos*remove-rejoins
    "Remove new proposals whose sources overlap in composition"
    (state <s> ^operator.name props-delta-combine
               ^combo <combo>)
    (<combo> ^source-delta <d1>
             ^source-delta {<d2> <> <d1>})
    (<d1> ^condition <c>)
    (<d2> ^condition <c>)
-->
    (<s> ^combo <combo> -)
}
sp {apply*props*delta-combine*combos*remove-duplicates
    "Remove new proposals that are the same as originals"
    (state <s> ^operator.name props-delta-combine
               ^superstate.props-epset.delta <sd>
               ^combo <combo>)
    (<combo> ^prop-apply <name>)
    (<sd> ^prop-apply <name>)
-->
    (<s> ^combo <combo> -)
}
sp {apply*props*delta-combine*combos*remove-subsumed
    "Remove components of existing proposals"
    (state <s> ^operator.name props-delta-combine
               ^superstate.props-epset.delta <sd>
               ^combo <combo>)
    (<combo> ^prop-apply <name>)
    (<sd> ^source-delta.prop-apply <name>)
-->
    (<s> ^combo <combo> -)
}
# Apply cycle 3: Return combos and end the substate
sp {apply*props*delta-tie*combine*step-3
    (state <s> ^operator.name props-delta-combine-combos
               ^combos-applied 2
              -^combos-applied 3)
-->
    (<s> ^combos-applied 3)
}
sp {apply*props*delta-combine*combos*return
    (state <s> ^operator.name props-delta-combine-combos
               ^superstate.props-epset <pe>
               ^combos-applied 2
               ^combo-literalization <cl>
               ^combo <combo>)
    (<combo> ^op-name )
    (<cl> ^combo <combo>
          ^literalize <l>)   # Condition on this to backtrace through the literalization of names
-->
    (<pe> ^delta <combo>)
}

sp {apply*props*delta-combine*combos*end-snc
    (state <s> ^operator.name props-delta-combine-combos
               ^quiescence t
               ^combos-applied 2
               ^superstate <ss> )
    (<ss> ^propose-indifferent <any>)
-->
    (<ss> ^propose-indifferent <any> -)
          #^no-propose true)
}
sp {apply*props*delta-combine*combos*new-deltas-done
    (state <s> ^operator.name props-delta-combine-combos
               ^quiescence t
               ^combos-applied 2
               ^combo <combo> )
-->
    (<combo> ^no-propose true)
}