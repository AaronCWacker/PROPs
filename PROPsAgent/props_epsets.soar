#####    Author: Bryan Stearns, 2018-2019
#   This code enables the use of delta sets at the topstate to guide instruction fetching.
#   This is intended to be an alternate to the fixed "manual" task sequence.
#   If sourced, the agent will load an epset (set of deltas / instructions / WM transition descriptions) to match the current task context.
#   This assumes a G.Gtask structure on the topstate, according to the Actransfer model.
#   This is intended to be a temporary assumption until deeper theory provides a more general context source.
#
##        OPERATORS
#   props-load-epset                        | Load a set of deltas for the task
#   props-retrieve-epset                    | Collect the smem retrieval results
#   <instructed-operator>                   | Propose any operators described by fetched epset instructions
#   props-epset-return                      | Return apply-buffer actions up to the superstate's apply-buffer
#   
##        STRUCTURE
#  (<s> ^G.Gtask <taskname>                 | The source of the epset name (task context)
#       ^topstate <ts>                      | The topstate pointer, elaborated to all states
#       ^const <c>                          | The object containing task-specific literals for a delta, elaborated here once the delta is selected
#       ^props-epset (<pe>                  | A set of instructed proposals, fetched from SMEM
#           ^props-epset-name <name>        | The unique identifier of this epset, usable for queries
#           ^const <c>                      | If this exists, this epset collects the general apply operators for a task-specific superstate operator
#          [^delta (<d>                     | Corresponds to a proposable operator
#               ^op-name <name>             | The proposal's operator name
#               ^prop-apply true            | A flag that indicates that this delta applies a parent operator; true if the flag exists
#               ^const <dc>                 | If this exists, this delta is for a specific task-specific operator proposal.
#              [^item-name <pname>]         | There will be one entry for every primitive action descended from this proposable operator in the hierarchy
#              [^prop (<prop>               | Corresponds to a single condition of the delta
#                   ^name <cname>           | The name of the primitive
#                   ^address1 <ts-attr1>    | A cached/provided attribute name from the topstate to find <id1>
#                   ^address2 <ts-attr2>    | A cached/provided attribute name from the topstate to find <id2>
#                   ^prop-type << ... >>    | The operation type
#                   ^attr1 <attr1>          | The attribute for arg1
#                   ^attr2 <attr2>)]        | The attribute for arg2
#               ^cond-id1 (<dcid1>          | Generated: The result of finding the id referenced by <ts>.<ts-attr1>  (SEE: props_auto_addressing.soar)
#                   ^name <cname>           | The name of the condition prop that this address is for
#                   ^id1 <id1>)             | The pointer to the specified address
#               ^cond-id2 (<dcid2>          | Generated: The result of finding the id referenced by <ts>.<ts-attr2>
#                   ^name <cname>           | The name of the condition prop that this address is for
#                   ^id2 <id2>)             | The pointer to the specified address
#               ^missing <what>]))          | Generated: Where <what> is << |id1| |id2| >>. Created if the id couldn't be found in WM.
#       ^prop-apply-buffer (<b>             | The apply buffer. (SEE: props_apply_buffer.soar)
#          [^apply (<ba>                    | A bundle of actions from a single operator
#              [^delta-action (<da>         | A single primitive action within the bundle
#                   ^name <name>)])]))      | The name of the primitive used
#####


# PROPAGATE THE TOPSTATE
sp {elaborate*topstate
    (state <s> ^superstate nil)
-->
    (<s> ^topstate <s>)
}
sp {elaborate*rootstate*cascade
    (state <s> ^superstate.topstate <ts>)
-->
    (<s> ^topstate <ts>)
}

# INHERIT CONST STRUCTS FROM SUPERSTATE EPSETS
sp {elaborate*epset*consts
    # Need to access consts in state the same way regardless of whether consts come from local epset or superstate epset, so that chunks will transfer
    (state <s> ^props-epset.const <c>)
-->
    (<s> ^const <c>)
}
sp {elaborate*superstate*consts
    (state <s> ^superstate.const <c>)
-->
    (<s> ^const <c>)
}



### OPERATOR ### 
# props-load-epset
##
# Fetch instructions for a set of operator proposals
### 

sp {propose*props*eval*query*epset*topstate
    "Load an epset to bias fetching for the task"
    (state <s> ^G.Gtask {<task> <> finish}    # Finish triggers interface response
              -^smem.result.<< retrieved failure >>
              -^props-epset.props-epset-name <task>)
-->
    (<s> ^operator <o> + >)
    (<o> ^name props-load-epset
         ^epset-name <task>)
}
sp {propose*props*eval*query*epset*substate
    "Load an epset to apply a parent delta (operator)"
    (state <s> ^superstate.operator <oo>
               ^impasse no-change
               ^attribute operator
              -^smem.result.<< retrieved failure >>
              -^props-epset)
    (<oo> ^delta 
         ^name <name>)
-->
    (<s> ^operator <o> + >)
    (<o> ^name props-load-epset
         ^epset-name <name>)        # Remove this line for free epset retrievals (should then prohibit used epsets for a time though)
}

sp {apply*props*eval*query*epset*task-named
    (state <s> ^operator <o>
               ^smem.command <scmd>)
    (<o> ^name props-load-epset
         ^epset-name <name>)
-->
    (<scmd> ^query <q>
            ^depth 3)
    (<q> ^props-epset-name <name>)
}
sp {apply*props*eval*query*epset*task-unnamed
    (state <s> ^operator <o>
               ^smem.command <scmd>)
    (<o> ^name props-load-epset
         -^epset-name <name>)
-->
    (<scmd> ^query <q>
            ^depth 3)
    (<q> ^props-epset-name <any>)
}


### OPERATOR ### 
# props-retrieve-epset
##
# Pull the query result
### 

sp {propose*props*retrieve-epset
    (state <s> ^smem.result.retrieved <e>
              -^props-epset.props-epset-name <name>)
    (<e> ^props-epset-name <name>)
-->
    (<s> ^operator <o> + >)
    (<o> ^name props-retrieve-epset)
}

sp {elaborate*props*retrieve-epset*failure
    (state <s> ^smem.result.failure <q>
               ^topstate.io.output-link <ol>)
    (<q> ^props-epset-name )
-->
    (<ol> ^error <e>)
    (<e> ^msg |EPSET RETRIEVAL FAILED| )
    (write |EPSET RETRIEVAL FAILED| (crlf) )
    (interrupt)
}

sp {apply*props*retrieve-epset*success*new
    (state <s> ^operator.name props-retrieve-epset
               ^smem <smem>
              -^props-epset)
    (<smem> ^command <scmd>
           ^result.retrieved <e>)
    (<scmd> ^query <q>)
-->
    (force-learn <s>)
    (<s> ^props-epset <e>)
    (<scmd> ^query <q> -)
}
sp {apply*props*retrieve-epset*success*replace
    (state <s> ^operator.name props-retrieve-epset
               ^smem <smem>
               ^props-epset {<olde> <> <e>})
    (<smem> ^command <scmd>
           ^result.retrieved <e>)
    (<scmd> ^query <q>)
-->
    (force-learn <s>)
    (<s> ^props-epset <e>
         ^props-epset <olde> -)
    (<scmd> ^query <q> -)
}



### OPERATOR ### 
# <instructed operator>
##
# Propose any deltas contained in a loaded epset that has all-true conditions.
### 

sp {propose*props*epset*delta*condition*all-true
    "Propose the operator if all conditions are true."
    (state <s> ^props-epset <pe>)
    (<pe> ^delta <d>)
    (<d> ^op-name <name>
        -^prop-apply)    # Only for conditions
    -{(<d> ^prop.name <cname>
          -^cond-success <cname>)} # It is not the case that this delta has untrue conditions
-->
    (<s> ^operator <o> + )    # Make indifferent to bypass proposal composition
    (<o> ^name <name>
         ^delta <d>)
}
sp {propose*props*epset*delta*action*all-true
    "Propose the operator if it hasnt been applied yet."
    (state <s> ^props-epset <pe>
              -^no-propose <d>    # Not if this was just created and about to be stored
               ^prop-apply-buffer <pb>)
    (<pe> ^delta <d>)
    (<d> ^prop-apply           # Only for actions
         ^op-name <dname>)
    -{(<pb> ^apply.delta-action.name <name>)
      (<d> ^item-name <name>)}  # It is not the case that this operator contains used actions
-->
    (<s> ^operator <o> + > =)    # Make indifferent to bypass proposal composition
    (<o> ^name <dname>
         ^delta <d>)
}



### OPERATOR ### 
# props-epset-return
##
# Flush the actions added to this state's apply-buffer to the superstate's apply-buffer.
# This will end the substate.
### 

sp {propose*props*epset-return
    (state <s> ^prop-apply-buffer <b>
               ^props-flag apply-buffer-nonempty
               ^superstate.operator.delta # For chunk condition
               ^props-epset <pe>)
  -{(<pe> ^delta.prop.name <cname>)
    (<b> -^apply.delta-action.name <cname>)}    # It is not the case that an operator delta has not been staged
-->
    (<s> ^operator <o> +)
    (<o> ^name props-epset-return)
}
sp {apply*props*epset-return
    (state <s> ^operator.name props-epset-return
               ^topstate <> <ss>
               ^superstate <ss>)
    (<ss> ^prop-apply-buffer.apply <sba>)
-->
    (<sba> ^dc-stamp (dc))
}
sp {apply*props*epset-return*delta-action
    (state <s> ^operator.name props-epset-return
               ^prop-apply-buffer.apply.delta-action <da>
               ^topstate <> <ss>
               ^superstate <ss>)
    (<ss> ^prop-apply-buffer.apply <sba>)
-->
    (<sba> ^delta-action <da>)
}
sp {apply*props*epset-return*to-topstate
    "When finished composing actions, simply return the apply buffer."
    (state <s> ^operator.name props-epset-return
               ^prop-apply-buffer.apply <pa>
               ^topstate <ss>
               ^superstate <ss>)
    (<ss> ^prop-apply-buffer <sb>)
-->
    (<sb> ^apply <pa>)
    (write | | (crlf) (crlf))
}

sp {set*apply-buffer*trim-dc-stamp
    "Remove old dc-stamps within the same apply struct"
    :o-support
    (state <s> ^prop-apply-buffer.apply <pa>)
    (<pa> ^dc-stamp <dc1>
          ^dc-stamp > <dc1>)
-->
    (<pa> ^dc-stamp <dc1> -)
}


sp {catch*props*failure*epset-return
    (state <s> ^impasse no-change
               ^attribute state
               ^topstate.io.output-link <ol>
               ^superstate <ss>) 
-->
    (write (cmd print <ss> -d 4))
    (write (cmd preferences <ss> operator --names))
    (<ol> ^error <e>)
    (<e> ^msg |EPSET NOT RETURNING| )
    (interrupt)
}